plugins {
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id 'com.google.cloud.tools.jib'
  id 'java'
  id 'org.unbroken-dome.helm'
}

version = '0.0.1-' + getCommitHash()

repositories {
  mavenCentral()
}

test {
  useJUnitPlatform()
}

dependencyManagement {
  imports {
    mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${springCloudAwsVersion}"
  }
}

dependencies {
  // EKS IRSA support
  implementation 'com.amazonaws:aws-java-sdk-sts'
  implementation project(':common-messaging')
  implementation project(':common-geoip')
  implementation project(':dns-crawler-dto')
  implementation project(':dns-crawler-persistence')

  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'io.micrometer:micrometer-registry-prometheus'

  // JSON logging
  implementation "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"

  // DB
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.flywaydb:flyway-core'
  runtimeOnly 'org.postgresql:postgresql'

  // explicitly declare a dependency on aws-java-sdk-core
  // otherwise we could get an older version in our classpath via common-messaging
  // seems to be an issue only when running from IntelliJ
  implementation 'com.amazonaws:aws-java-sdk-core'

  // Lombok
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  implementation 'dnsjava:dnsjava:3.3.1'

  implementation 'com.maxmind.geoip2:geoip2:2.12.0'

  // https://mvnrepository.com/artifact/org.apache.commons/commons-compress
  implementation 'org.apache.commons:commons-compress:1.21'

  // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
  implementation 'org.apache.commons:commons-lang3:3.12.0'

  // https://mvnrepository.com/artifact/com.google.guava/guava
  implementation 'com.google.guava:guava:28.2-jre'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation testFixtures(project(':common-testing'))
}

jib {
  container {
    jvmFlags = ['-XX:MaxRAMPercentage=75.0']
  }
}
