plugins {
  id 'base'
  id "com.moowork.node" version "1.3.1"
  id 'org.unbroken-dome.helm'
}

version = '0.0.1-' + getCommitHash()

npm_install {
  args = ['--force']
}

//assemble.dependsOn npm_run_build
assemble.dependsOn npm_install

tasks.register('dockerBuild', Exec) {
  def registry = ""
  if (project.hasProperty("dockerRegistry")) {
    registry = project.getProperty("dockerRegistry")
  }
  def tags = "-t ${registry}dnsbelgium/mercator/${project.name}:${project.version} -t ${registry}dnsbelgium/mercator/${project.name}:${getCommitHash()} -t ${registry}dnsbelgium/mercator/${project.name}:${getCommitHash()}"
  if (registry == "") {
    tags += " -t dnsbelgium/mercator/${project.name}:local"
  }
  if (project.hasProperty("dockerTags")) {
    project.getProperty('dockerTags').split(',').each {
      tags += " -t ${registry}dnsbelgium/mercator/${project.name}:$it"
    }
  }

  executable 'sh'
  args '-c', "docker build $tags ."
}

tasks.register('dockerPush', Exec) {
  def registry = ""
  if (project.hasProperty("dockerRegistry")) {
    registry = project.getProperty("dockerRegistry")
  }

  def tags = [getCommitHash()]
  if (project.hasProperty("dockerTags")) {
    project.getProperty('dockerTags').split(',').each {
      tags += [it]
    }
  }

  def command = "docker push ${registry}dnsbelgium/mercator/${project.name}:${project.version}"
  tags.each {
    command += "&& docker push ${registry}dnsbelgium/mercator/${project.name}:${it}"
  }

  executable 'sh'
  args '-c', command
}

tasks.register('dockerBuildAndPush') {
  dependsOn tasks.dockerBuild, tasks.dockerPush
}

helm.charts {
  main {
    sourceDir = file('helm')
  }
}
